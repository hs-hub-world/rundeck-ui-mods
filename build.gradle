defaultTasks 'clean', 'build'

ext.pluginName = 'rundeck-ui-mods'
ext.pluginAuthor = 'Trevor Highfill'
ext.pluginBuildDate = new Date().format("yyyy-MM-dd'T'HH:mm:ssX")
ext.pluginVersion = '1.0.0'
ext.pluginUrl = 'https://github.com/theque5t/rundeck-ui-mods'

ext.pluginBaseName = "$project.name"
ext.pluginFileName = ext.pluginBaseName + '-' + ext.pluginVersion + '-plugin'
ext.pluginZipName = ext.pluginFileName + '.zip'

task createPluginRootDir{
    mkdir "${buildDir}/${project.ext.pluginFileName}"
}

task copyPlugin (dependsOn: 'createPluginRootDir', type: Copy){
    from("${projectDir}") {
        include 'plugin.yaml'
        expand(pluginName: "${project.ext.pluginName}",
               pluginAuthor: "${project.ext.pluginAuthor}",
               pluginBuildDate: "${project.ext.pluginBuildDate}",
               pluginVersion: "${project.ext.pluginVersion}",
               pluginUrl: "${project.ext.pluginUrl}")
    }
    from (fileTree(dir: "${projectDir}")){
        exclude('build')
        exclude('docs')
        exclude('plugin.yaml')
        exclude('README.md')
        exclude('**/*.gradle')
    }
    into "${buildDir}/${project.ext.pluginFileName}"
}

task zipPlugin(dependsOn: 'copyPlugin', type: Zip){
    from fileTree(dir: "${buildDir}")
    exclude('**/*.zip')
    archiveName "${project.ext.pluginZipName}"
    destinationDir(file("${buildDir}"))
} 

task cleanBuildExcess(dependsOn: 'zipPlugin', type: Delete){
    delete "${buildDir}/${project.ext.pluginFileName}"
}

task build(dependsOn: 'cleanBuildExcess'){
}

task clean(type: Delete){
    delete "${buildDir}"
}